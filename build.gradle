plugins {
    id 'groovy'
    id 'signing'
    id 'maven-publish'
}

group 'io.github.segment11'
version '1.2-SNAPSHOT'

final String artifactId = 'segment_common'

repositories {
    maven {
        url 'https://maven.aliyun.com/nexus/content/groups/public/'
    }
    mavenCentral()
}

sourceSets {
    main {
        groovy {
            srcDirs = ['src']
        }
        resources {
            srcDirs = ['src']
        }
    }
    test {
        groovy {
            srcDirs = ['test']
        }
        resources {
            srcDirs = ['test']
        }
    }
}

sourceCompatibility = 17
targetCompatibility = 17

compileGroovy {
    groovyOptions.configurationScript = file("config.groovy")
}

dependencies {
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'
    implementation group: 'org.apache.groovy', name: 'groovy', version: '4.0.12'

    implementation group: 'com.alibaba', name: 'fastjson', version: '1.2.83'

    implementation group: 'commons-net', name: 'commons-net', version: '3.6'

    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.14.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.14.1'
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.14.1'

    implementation group: 'com.github.kevinsawicki', name: 'http-request', version: '6.0'

    testImplementation group: 'org.spockframework', name: 'spock-core', version: '2.3-groovy-4.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.20.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.20.0'
    testImplementation group: 'org.apache.logging.log4j', name: 'log4j-slf4j-impl', version: '2.20.0'
    testImplementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.49.v20220914'
    testImplementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.49.v20220914'
}

test {
    useJUnitPlatform()
}

def ossReleaseUrl = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2'
def ossSnapshotUrl = 'https://s01.oss.sonatype.org/content/repositories/snapshots'
//ossSnapshotUrl = ossReleaseUrl

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
//            artifactId = name
            groupId = group
            version = version

            pom {
                name = 'segmentweb'
                description = 'Common tools for other projects.'
                url = 'https://github.com/segment11/segmentweb'
                licenses {
                    license {
                        name = 'The Apache Software License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'segment11'
                        name = 'Yong Ding'
                        email = 'dingyong87@163.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'ossrh'
            url = version.endsWith('SNAPSHOT') ? ossSnapshotUrl : ossReleaseUrl
            credentials {
                username = System.getProperty('ossrh.username')
                password = System.getProperty('ossrh.password')
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

